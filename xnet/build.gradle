apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'
apply plugin: 'de.undercouch.download'

android {
    compileSdkVersion 28
    ndkVersion "21.0.6113669"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
    }

    buildTypes {
        debug {
            ndk {
                abiFilters 'armeabi-v7a'
            }
        }

        release {
            ndk {
                abiFilters 'armeabi-v7a', 'x86', 'arm64-v8a', 'x86_64'
            }

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

task setHttpProxyFromEnv {
    def map = ['HTTP_PROXY': 'http', 'HTTPS_PROXY': 'https']
    for (e in System.getenv()) {
        def key = e.key.toUpperCase()
        def base = map[key]
        if (base?.value) {
            def url = e.value.toURL()
            println " - systemProp.${base}.proxy=${url.host}:${url.port}"
            System.setProperty("${base}.proxyHost", url.host.toString())
            System.setProperty("${base}.proxyPort", url.port.toString())
        }
    }
}

task downloadZipFile(type: Download) {
    src 'https://xnet-android-1255868781.cos.ap-guangzhou.myqcloud.com/XP2P_CLIENT_EGAME_v8.01.01/XP2P_CLIENT_EGAME_v8.01.01_so.zip'
    dest new File(buildDir, 'so.zip')
    onlyIfModified true
}

task downloadAndUnzipFile(dependsOn: downloadZipFile, type: Copy) {
    from zipTree(downloadZipFile.dest)
    into "$rootDir/xnet/src/main/jniLibs"
}

preBuild.dependsOn setHttpProxyFromEnv, downloadAndUnzipFile

publish {
    repoName = 'xnet-android-sdk'
    userOrg = 'tencentyun'
    groupId = 'com.tencent.qcloud'
    artifactId = 'xnet'
    publishVersion = '1.0.2'
    desc = 'xnet android sdk'
    website = 'https://github.com/tencentyun/xnet-android-sdk'
}

bintrayUpload.dependsOn setHttpProxyFromEnv
